plugins {
    id 'java'
    id 'antlr'
}

def gitDescribe() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always', '--dirty'
        standardOutput = stdout
        errorOutput = new ByteArrayOutputStream()
        ignoreExitValue = true
    }
    def s = stdout.toString('UTF-8').trim()
    return s ?: "unknown"
}

group = 'com.koyomiji.asteria'
version = gitDescribe()

repositories {
    mavenCentral()
}

dependencies {
    def antlrVersion = '4.13.2'
    antlr "org.antlr:antlr4:$antlrVersion"
    implementation "org.antlr:antlr4-runtime:$antlrVersion"
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

generateGrammarSource {
    arguments += ["-package", "com.koyomiji.asteria.parser"]
    arguments += ["-visitor", "-no-listener"]
}